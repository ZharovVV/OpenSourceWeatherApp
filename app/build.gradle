plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'com.google.gms.google-services'
}

android {
    compileSdkVersion 31
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "com.github.zharovvv.open.source.weather.app"
        minSdkVersion 23
        targetSdkVersion 31
        versionCode 4
        versionName "1.0.2"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            Properties properties = new Properties()
            if (project.rootProject.file("local.properties").canRead()) {
                properties.load(project.rootProject.file("local.properties").newDataInputStream())
            }
            buildConfigField "String", "WEATHER_API_KEY", properties.getProperty("openweathermap.org.api.keys", "")
            applicationIdSuffix ".debug"
            debuggable true
//            minifyEnabled true
//            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        release {
            buildConfigField "String", "WEATHER_API_KEY", ""
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        viewBinding true
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    //AAC
    //for delegating ViewModel (by viewModels())
    implementation "androidx.activity:activity-ktx:1.4.0"
    //WorkManager
    implementation "androidx.work:work-runtime:2.7.1"
    implementation "androidx.work:work-runtime-ktx:2.7.1"
    //Navigation AC
    implementation 'androidx.navigation:navigation-fragment-ktx:2.4.1'
    implementation 'androidx.navigation:navigation-ui-ktx:2.4.1'
    //AndroidX Preference Library
    implementation "androidx.preference:preference-ktx:1.2.0"
    //RxJava
    implementation "io.reactivex.rxjava2:rxjava:$rxjava2_version"
    implementation "io.reactivex.rxjava2:rxandroid:$rxjava2_rxandroid_version"
    implementation "io.reactivex.rxjava2:rxkotlin:$rxjava2_rxkotlin_version"
    //Retrofit
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.okhttp3:logging-interceptor:3.14.9"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_gson_converter_version"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofit_rxjava2_adapter_version"
    //Gson
    implementation "com.google.code.gson:gson:$gson_version"
    //Room
    kapt "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-rxjava2:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    //Apache Commons
    implementation "org.apache.commons:commons-text:$apache_commons_text_version"
    // debugImplementation because LeakCanary should only run in debug builds.
    debugImplementation "com.squareup.leakcanary:leakcanary-android:2.7"

    //Dagger 2
    implementation "com.google.dagger:dagger:$dagger_2_version"
    //Java annotationProcessor "..."
    kapt "com.google.dagger:dagger-compiler:$dagger_2_version"

    //Firebase
    implementation platform('com.google.firebase:firebase-bom:29.2.0')
    implementation 'com.google.firebase:firebase-config-ktx'
    implementation 'com.google.firebase:firebase-analytics-ktx'
}